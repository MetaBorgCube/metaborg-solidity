module Common

context-free start-symbols
  STRING
  HEXSTRING
  NUMBER

lexical syntax

  ID             = [a-zA-Z\_\$] [a-zA-Z0-9\_\$]*
  PATH           = ID ("." ID)*
  
  INT            = [0-9]+ 
  FRACT          = ("." [0-9]+) | ([0-9]+ ".") | ([0-9]+ "." [0-9]+)
  HEXINT         = [0-9a-fA-F]+ //"0x" [0-9a-fA-F]+
  
  SCIENTIFIC     = (INT | FRACT) 'e' INT
  
  NumberUnit     = "wei" | "szabo" | "finney" | "ether" | "seconds" | "minutes" | "hours" | "days" | "weeks" | "years"
  
  //Strings
  STRING         = DSTRING
  STRING         = SSTRING
  
  DSTRING        = "\"" DStringChar* "\"" 
  DStringChar    = ~[\"\n] 
  DStringChar    = "\\\"" 
  DStringChar    = BackSlashChar 
  BackSlashChar  = "\\"
  
  SSTRING        = "'" SStringChar* "'"
  SStringChar    = ~[\'\n]
  SStringChar    = "\\'"
  SStringChar    = BackSlashChar
  
  //Hex strings
  HEXSTRING      = "hex" "\"" (HexStringChar HexStringChar)* "\""
  HEXSTRING      = "hex" "'" (HexStringChar HexStringChar)* "'"
  HexStringChar  = [0-9A-Fa-f]
  
  //Comments
  LAYOUT         = [\ \t\n\r] 
  CommentChar    = [\*] 
  LAYOUT         = "/*" InsideComment* "*/" 
  InsideComment  = ~[\*] 
  InsideComment  = CommentChar 
  LAYOUT         = "//" ~[\n\r]* NewLineEOF 
  NewLineEOF     = [\n\r] 
  NewLineEOF     = EOF 
  EOF            =  

  ID = KEYWORD {reject}
  KEYWORD = "abstract"
  KEYWORD = "after"
  KEYWORD = "case"
  KEYWORD = "catch"
  KEYWORD = "default"
  KEYWORD = "final"
  KEYWORD = "in"
  KEYWORD = "inline"
  KEYWORD = "let"
  KEYWORD = "match"
  KEYWORD = "null"
  KEYWORD = "of"
  KEYWORD = "relocatable"
  KEYWORD = "static"
  KEYWORD = "switch"
  KEYWORD = "try"
  KEYWORD = "type"
  KEYWORD = "typeof"

lexical restrictions

  // Ensure greedy matching for lexicals
  
  CommentChar   -/- [\/]
  INT           -/- [0-9]
  HEXINT        -/- [0-9a-fA-F]
  FRACT         -/- [0-9\.]
  ID            -/- [a-zA-Z0-9\_\$]
  PATH          -/- [a-zA-Z0-9\_\$\.] //TODO
  
  // EOF may not be followed by any char
  
  EOF           -/- ~[]
  
  // Backslash chars in strings may not be followed by " 
  
  BackSlashChar -/- [\"]

context-free restrictions

  // Ensure greedy matching for comments
  
  LAYOUT? -/- [\ \t\n\r]
  LAYOUT? -/- [\/].[\/]
  LAYOUT? -/- [\/].[\*]

syntax
  NUMBER-CF.Int        = INT-CF
  NUMBER-CF.Decimal    = FRACT-CF
  NUMBER-CF.Scientific = SCIENTIFIC-CF
  NUMBER-CF.HexInt     = "0x" HEXINT-CF
