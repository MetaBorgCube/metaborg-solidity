module statements

imports
  Common
  expressions
  modifiers
  tuples
  types
  vars
  assembly

template options
  tokenize : "({;."
  keyword -/- [a-zA-Z0-9]

lexical syntax
  //Add keywords
  KEYWORD = STMTKEYWORD
  STMTKEYWORD = "continue"
  STMTKEYWORD = "break"
  STMTKEYWORD = "throw"
  STMTKEYWORD = "if"
  STMTKEYWORD = "else"
  STMTKEYWORD = "while"
  STMTKEYWORD = "do"
  STMTKEYWORD = "for"
  STMTKEYWORD = "emit"
  STMTKEYWORD = "return"
  
context-free start-symbols
  Statement

context-free syntax
  //-----------------------------------------------------------------------------------------------
  //Control Flow
  Statement.Block    = <
    {
      <{Statement "\n"}*>
    }>
  Statement.If       = < if (<Exp>) <Statement> <Else>>
  Else.None          = <>
  Else.Else          = < else <Statement> >

  Statement.While    = < while (<Exp>) <Statement> >
  Statement.DoWhile  = < do <Statement> while (<Exp>); >
  
  //The first statement of for can be either ";", a variable declaration or an expression statement
  Statement.For      = < for (<ForStatement> <Exp?>; <Exp?>) <Statement> >
  ForStatement.None  = <;>
  ForStatement.SomeA = VarDeclStatement
  ForStatement.SomeB = ExpStatement
  
  Statement.Continue = <continue;>
  Statement.Break    = <break;>
  Statement.Throw    = <throw;>
  Statement.Emit     = <emit <FunctionCall>;>
  
  //-----------------------------------------------------------------------------------------------
  //Local variable declaration
  VarDeclStatement.DeclareVar            = <<LocalVarType> <ModVarDecl> <ID>;>
  VarDeclStatement.DeclareVarAssign      = <<LocalVarType> <ModVarDecl> <ID> = <Exp>;>
  VarDeclStatement.DeclareTupleVarAssign = <<VarType> (<{TupleName ", "}+>) = <Exp>;>
  Statement                              = VarDeclStatement
  
  ModVarDecl.NoModifier = <>
  ModVarDecl = ModLocation
  
  //-----------------------------------------------------------------------------------------------
  //Underscore statement (modifiers)
  UnderscoreStatement.Underscore = <_;> {prefer}
  Statement                      = UnderscoreStatement
  
  //-----------------------------------------------------------------------------------------------
  //Arbitrary Expressions
  ExpStatement.ExpStmt = <<Exp>;>
  Statement            = ExpStatement
  
  //-----------------------------------------------------------------------------------------------
  //Return statements
  ReturnStatement.VoidReturn      = <return;>
  ReturnStatement.ReturnStatement = <return <Exp>;>
  Statement                       = ReturnStatement
  
  //-----------------------------------------------------------------------------------------------
  //Inline assembly
  Statement = AssemblyStatement

context-free priorities
  UnderscoreStatement.Underscore > ExpStatement.ExpStmt
