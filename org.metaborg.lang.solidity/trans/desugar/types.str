module desugar/types

imports
  signatures/types-sig
  signatures/expressions-sig
  signatures/Common-sig
  utils/externals

signature
  sorts


rules
  //Default values
  desugar: IntType(None())    -> IntType(256)
  desugar: UIntType(None())   -> UIntType(256)
  desugar: FixedType(None())  -> FixedType(MxN(128, 18))
  desugar: UFixedType(None()) -> UFixedType(MxN(128, 18))
  desugar: ByteType()         -> FixedBytesArrayType(1)

  //Convert the strings to ints
  desugar: IntType(Some(x))      -> IntType(<string-to-int> x)
  desugar: UIntType(Some(x))     -> UIntType(<string-to-int> x)
  desugar: FixedType(MxN(m, n))  -> FixedType(MxN(mI, nI))
    where
    mI := <string-to-int> m;
    nI := <string-to-int> n
  desugar: UFixedType(MxN(m, n)) -> UFixedType(MxN(mI, nI))
    where
    mI := <string-to-int> m;
    nI := <string-to-int> n


  desugar: FixedBytesArrayType(s) -> FixedBytesArrayType(<string-to-int> s)

  //Remove the some/none syntax from functiontype return types
  desugar: FunctionType(args, vis, mut, None())                  -> FunctionType(args, vis, mut, [])
  desugar: FunctionType(args, vis, mut, Some(ReturnsType(rets))) -> FunctionType(args, vis, mut, rets)

  //-----------------------------------------------------------------------------------------------
  //Convert back to original AST
  resugar: IntType(n)                       -> IntType(Some(<int-to-string> n))
  resugar: UIntType(n)                      -> UIntType(Some(<int-to-string> n))
  resugar: FixedType(MxN(m, n))             -> FixedType(MxN(<int-to-string> m, <int-to-string> n))
  resugar: UFixedType(MxN(m, n))            -> UFixedType(MxN(<int-to-string> m, <int-to-string> n))
  resugar: FixedBytesArrayType(s)           -> FixedBytesArrayType(<int-to-string> s)
  resugar: FunctionType(a, v, m, [])        -> FunctionType(a, v, m, None())
  resugar: FunctionType(a, v, m, r@[_ | _]) -> FunctionType(a, v, m, Some(ReturnsType(r)))