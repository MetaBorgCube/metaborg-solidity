module desugar/strings

imports
  signatures/expressions-sig
  
signature
  constructors
    StringLit : String * INT -> Exp

rules
  desugar = desugar-stringlit
  
  /**
   * Add a length to string literals.
   * The length is -1 if the string is longer than 32 characters.
   */
  desugar-stringlit: StringLit(x) -> StringLit(x', y)
    where
    (
      (<string-starts-with(|"\"")> x; x':= <un-double-quote> x) <+
      (<string-starts-with(|"'")> x; x':= <un-single-quote> x)
    );
    len := <strlen> x';
    (
      (<gt> (len, 32); y := -1) <+
      (y := len)
    )
  
  //-----------------------------------------------------------------------------------------------
  //Convert back to original AST
  resugar = resugar-stringlit
  
  resugar-stringlit: StringLit(x, _) -> StringLit(x)