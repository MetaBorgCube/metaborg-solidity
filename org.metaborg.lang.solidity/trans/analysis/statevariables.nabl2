module analysis/statevariables

imports
  signatures/-

rules
  [[ StateVariable(solty, mod, name, None()) ^ (s) ]] :=
    [[ solty ^ (s) : tyStateVar ]],
    [[ mod   ^ (s) ]],
    
    Var{name} <- s,
    Var{name}.origin   := StateVariable(),
    Var{name}.constant := FalseC(),
    Var{name} : tyStateVar !.
  
  [[ StateVariable(solty, mod, name, Some(exp)) ^ (s) ]] :=
    [[ solty ^ (s) : tyStateVar ]],
    [[ mod   ^ (s) ]],
    
    Var{name} <- s,
    Var{name}.origin   := StateVariable(),
    Var{name}.constant := FalseC(),
    Var{name} : tyStateVar !,
    
    [[ exp ^ (s)    : tyAssignExp ]],
    tyAssignExp <sub? tyStateVar | error $[Cannot assign expression of type [tyAssignExp] to a variable of type [tyStateVar]] @exp.
  
  [[ ConstantStateVariable(solty, mod, name, None()) ^ (s) ]] :=
    [[ solty ^ (s) : tyStateVar ]],
    [[ mod   ^ (s) ]],
    
    Var{name} <- s,
    Var{name}.origin   := StateVariable(),
    Var{name}.constant := TrueC(),
    Var{name} : tyStateVar !,
    
    false | warning $[Constants should have initializers!].

  [[ ConstantStateVariable(solty, mod, name, Some(exp)) ^ (s) ]] :=
    [[ solty ^ (s) : tyStateVar ]],
    [[ mod   ^ (s) ]],
    
    Var{name} <- s,
    Var{name}.origin   := StateVariable(),
    Var{name}.constant := TrueC(),
    Var{name} : tyStateVar !,
    
    [[ exp ^ (s)    : tyAssignExp ]],
    tyAssignExp <sub? tyStateVar | error $[Cannot assign expression of type [tyAssignExp] to a variable of type [tyStateVar]] @exp.
