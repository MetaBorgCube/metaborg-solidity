module codegen/statements

imports
  codegen/expressions
  codegen/types
  
  nabl2/api
  
  signatures/ebc/-
  ebc/ebcshared/ebcfunctions
  ebc/ebcshared/codegen
  analysis/types

  signatures/statements-sig
  signatures/expressions-sig
  desugar/-
  
  utils/externals
  
  libspoofax/stratego/debug

rules
  //-----------------------------------------------------------------------------------------------
  //Statements
  //-----------------------------------------------------------------------------------------------
  
  /**
   * Converts a statement to the corresponding ebc instructions.
   *
   * @param  stack               - the stack
   * @param  stack               - a stack of tags used for continue
   * @param  stack               - a stack of tags used for breaks
   * @param  statement (current) - the statement to convert
   * @return list(instruction)   - the ebc instructions that represent the statement
   */
// stmt-to-ebc(|stack, continues, breaks)
  
  //-----------------------------------------------------------------------------------------------
  //Blocks
  //-----------------------------------------------------------------------------------------------
  
  stmt-to-ebc(|stack, c, b): Block(stmts) -> <mapconcat(stmt-to-ebc(|stack, c, b))> stmts
  
  //-----------------------------------------------------------------------------------------------
  //Expression statements
  //-----------------------------------------------------------------------------------------------
  
  stmt-to-ebc(|stack, c, b): ExpStmt(e1) -> <nopush-exp-to-ebc(|stack)> e1
  
  //-----------------------------------------------------------------------------------------------
  //If statements
  //-----------------------------------------------------------------------------------------------
  
  stmt-to-ebc(|stack, c, b): If(condExp, trueStmt, Else(elseStmt)) -> instr
    where
    tagFalse   := <newname> "If_ElseBranch";
    tagAfterIf := <newname> "If_After";
    condition  := <exp-to-ebc(|stack)> condExp; ebc-stack-pop(|stack);
    ifBranch   := <stmt-to-ebc(|stack, c, b)> trueStmt;
    elseBranch := <stmt-to-ebc(|stack, c, b)> elseStmt;
    instr := <concat> [           //
      condition,                  //  PUSH condition
      [ ISZERO(),                 //  ISZERO
        PUSHTAG(tagFalse),        //  PUSHTAG tagFalse
        JUMPI() ],                //  JUMPI
      ifBranch,                   //  <IF-BRANCH>
      [ PUSHTAG(tagAfterIf),      //  PUSHTAG tagAfterIf
        JUMP(),                   //  JUMP
                                  //:false
        JUMPDESTTAG(tagFalse) ],  //  JUMPDEST
      elseBranch,                 //  <ELSE-BRANCH>
      [                           //:afterIf
        JUMPDESTTAG(tagAfterIf) ] //  JUMPDEST
    ]
  
  stmt-to-ebc(|stack, c, b): If(condExp, trueStmt, None()) -> instr
    where
    tagAfterIf := <newname> "If_After";
    condition  := <exp-to-ebc(|stack)> condExp; ebc-stack-pop(|stack);
    ifBranch   := <stmt-to-ebc(|stack, c, b)> trueStmt;
    instr := <concat> [           //
      condition,                  //  PUSH condition
      [ ISZERO(),                 //  ISZERO
        PUSHTAG(tagAfterIf),      //  PUSHTAG tagAfterIf
        JUMPI() ],                //  JUMPI
      ifBranch,                   //  <IF-BRANCH>
      [                           //:afterIf
        JUMPDESTTAG(tagAfterIf) ] //  JUMPDEST
    ]
  
  //-----------------------------------------------------------------------------------------------
  //Loops
  
  //TODO For loops
  stmt-to-ebc(|stack, continues, breaks): While(e, body) -> instr
    where
    tagLoop       := <newname> "While_Loop";
    tagAfterWhile := <newname> "While_After";
    condition := <exp-to-ebc(|stack)> e; ebc-stack-pop(|stack);
    
    continues' := [tagLoop | continues];
    breaks'    := [tagAfterWhile | breaks];
    bodyInstr := <stmt-to-ebc(|stack, continues', breaks')> body;
    
    instr := <concat> [
      [                              //:loop
        JUMPDESTTAG(tagLoop) ],      //  JUMPDEST
      condition,                     //  PUSH condition
      [ ISZERO(),                    //  ISZERO
        PUSHTAG(tagAfterWhile),      //  PUSHTAG tagAfterWhile
        JUMPI() ],                   //  JUMPI
      bodyInstr,                     //  <body>
      [ PUSHTAG(tagLoop),            //  PUSHTAG tagLoop
        JUMP(),                      //  JUMP
                                     //:afterWhile
        JUMPDESTTAG(tagAfterWhile) ] //  JUMPDEST
    ]
  
  stmt-to-ebc(|stack, continues, breaks): DoWhile(body, e) -> instr
    where
    tagLoop       := <newname> "DoWhile_Loop";
    tagCondition  := <newname> "DoWhile_Condition";
    tagAfterWhile := <newname> "DoWhile_After";
    condition := <exp-to-ebc(|stack)> e; ebc-stack-pop(|stack);
    
    continues' := [tagCondition | continues];
    breaks'    := [tagAfterWhile | breaks];
    bodyInstr := <stmt-to-ebc(|stack, continues', breaks')> body;
    
    instr := <concat> [
      [                              //:loop
        JUMPDESTTAG(tagLoop) ],      //  JUMPDEST
      bodyInstr,                     //  <body>
      [                              //:condition  (for continue)
        JUMPDESTTAG(tagCondition) ], //  JUMPDEST
      condition,                     //  PUSH condition
      [ ISZERO(),                    //  ISZERO
        ISZERO(),                    //  ISZERO
        PUSHTAG(tagLoop),            //  PUSHTAG tagLoop
        JUMPI(),                     //  JUMPI
                                     //:afterWhile
        JUMPDESTTAG(tagAfterWhile) ] //  JUMPDEST
    ]
  
  //-----------------------------------------------------------------------------------------------
  //Control flow
  //-----------------------------------------------------------------------------------------------
  
  stmt-to-ebc(|stack, c, b): ReturnStatement(name, e1) -> instr
    where
    instr := <concat> [
      <exp-to-ebc(|stack)> e1,
      //Work on a copy of the stack to ensure that no changes are made.
      <ebc-retain-value-at(|<ebc-stack-copy(|stack)>)> 0,
      [ SWAP(1),
        JUMP() ]
    ];
    ebc-stack-pop(|stack)
  
  stmt-to-ebc(|stack, c, b): VoidReturn(name) -> <concat> [
    //Work on a copy of the stack to ensure that no changes are made.
    <ebc-stack-clear(|<ebc-stack-copy(|stack)>)>,
    [ SWAP(1),
      JUMP() ]
  ] where
    a := <nabl2-get-ast-analysis>;
    ref-occ := <nabl2-mk-occurrence(|"Function")> name;
    dec-occ := <nabl2-get-resolved-name(|a); Fst> ref-occ;
    type    := <nabl2-get-type(|a)> dec-occ;
    ret-type := <return-type> type
  
  stmt-to-ebc(|stack, continues, b): Continue() -> [ PUSHTAG(tag), JUMP() ]
    where
    tag := <ebc-stack-pop(|continues)>
  
  stmt-to-ebc(|stack, c, breaks): Break() -> [ PUSHTAG(tag), JUMP() ]
    where
    tag := <ebc-stack-pop(|breaks)>

  //Throw and emit are not yet supported
  
  //-----------------------------------------------------------------------------------------------
  //Variable declaration
  
  //These are already handled by var-to-stack (see functions.str)
  stmt-to-ebc(|stack, c, b): d@DeclareVar(_, _, _) -> []
    with <debug(|"Encountered unexpected VarDecl, ignoring: ")> d
  stmt-to-ebc(|stack, c, b): d@DeclareVarAssign(_, _, x, v) -> <stmt-to-ebc(|stack, c, b)> ExpStmt(Assign(x, v))
    with <debug(|"Encountered unexpected VarDeclAssign: ")> d
  stmt-to-ebc(|stack, c, b): d@DeclareVarVar(x, v) -> <stmt-to-ebc(|stack, c, b)> ExpStmt(Assign(x, v))
    with <debug(|"Encountered unexpected VarDeclAssign: ")> d
  
  //-----------------------------------------------------------------------------------------------
  //Underscore
  
  //Underscore statement is not yet supported
  